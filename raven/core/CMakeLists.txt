string(TOUPPER ${RAVENCM_MOD_NAME} RAVENCM_MOD_MODNAMEUP)

configure_file(
                "${CMAKE_SOURCE_DIR}/raven/configuration_project.h.in"
                "${CMAKE_BINARY_DIR}/configuration_${RAVENCM_MOD_NAME}.h"
        )

set(RAVENCM_MOD_FINALDEFINES ${RAVENCM_GLOBAL_DEFINES} ${RAVENCM_MOD_DEFINES} "-DRAVEN_${RAVENCM_MOD_MODNAMEUP}_EXPORTS")
set(RAVENCM_MOD_FINALINCLUDEDIRS ${RAVENCM_GLOBAL_INCLUDEDIRS} ${RAVENCM_MOD_INCLUDEDIRS})
set(RAVENCM_MOD_FINALLIBRARYDIRS ${RAVENCM_GLOBAL_LIBRARYDIRS} ${RAVENCM_MOD_LIBRARYDIRS})
set(RAVENCM_MOD_FINALLIBRARIES ${RAVENCM_MOD_LIBRARIES} ${RAVENCM_MOD_DEPENDMODULES} ${RAVENCM_MOD_PLUGINS})

# 收集项目的所有源文件
set(RAVENCM_MOD_SOURCES "")
file(GLOB_RECURSE RAVENCM_MOD_SOURCES 
        RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} 
        ${RAVENCM_DEF_SOURCEEXTS}
    )
    
# 把收集到的代码文件按所在文件夹分组
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${RAVENCM_MOD_SOURCES})

# 加入工程目标
if(RAVENCM_MOD_MODULEMODE EQUAL 0)
    add_library(${RAVENCM_MOD_NAME} ${RAVENCM_MOD_SOURCES})
elseif(RAVENCM_MOD_MODULEMODE EQUAL 1)
    add_executable(${RAVENCM_MOD_NAME} ${RAVENCM_MOD_SOURCES})
elseif(RAVENCM_MOD_MODULEMODE EQUAL 2)
    add_library(${RAVENCM_MOD_NAME} SHARED ${RAVENCM_MOD_SOURCES})
elseif(RAVENCM_MOD_MODULEMODE EQUAL 3)
    add_library(${RAVENCM_MOD_NAME} STATIC ${RAVENCM_MOD_SOURCES})
else()
    message("ERROR: unsupported module mode.")
endif()

if(RAVENCM_MOD_FINALDEFINES)
    target_compile_definitions(${RAVENCM_MOD_NAME} PRIVATE ${RAVENCM_MOD_FINALDEFINES})
endif()

if(RAVENCM_MOD_FINALINCLUDEDIRS)
    target_include_directories(${RAVENCM_MOD_NAME} PRIVATE ${RAVENCM_MOD_FINALINCLUDEDIRS})
endif()

if(RAVENCM_MOD_FINALLIBRARYDIRS)
    target_link_directories(${RAVENCM_MOD_NAME} PRIVATE ${RAVENCM_MOD_FINALLIBRARYDIRS})
endif()

# 在这里加入需要连接的库文件，如果有的话
if(RAVENCM_MOD_FINALLIBRARIES)
	target_link_libraries(${RAVENCM_MOD_NAME} ${RAVENCM_MOD_FINALLIBRARIES})
endif()